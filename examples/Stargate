
set Stargate::Chevrons {
	1 1
	0 3
	1 6
	6 6
	7 3
	6 1
	3 7
}

set Stargate::LEDs {
	2 0 1 1
	0 2 0 3 0 4 0 5
	1 6
	2 7 3 7 4 7 5 7
	6 6
	7 5 7 4 7 3 7 2
	6 1
	5 0 4 0 3 0
}

proc Stargate::Blink {{off 0}} {{blink 0}} {
	if {$off} {
		set c [cube.colour -get black]
		incr blink -1
	} elseif {$blink >= 0 && $blink < 4} {
		set c [cube.colour -get red]
	} else {
		if {$blink > 0} { set blink -3 }
		set c [cube.colour -get black]
	}
	cube.led 0 0 0 {*}$c
	cube.led 0 0 7 {*}$c
	cube.led 7 0 0 {*}$c
	cube.led 7 0 7 {*}$c
	incr blink
}

proc Stargate::Dial {delay} {
	cube.colour 52 11 0
	foreach {x z} $::Stargate::Chevrons {
		foreach {i j} $::Stargate::LEDs {
			::Stargate::Blink
			cube.led $i 0 $j
			delay 50
			cube.led $i 0 $j 0 0 0
		}
		foreach {i j} $::Stargate::LEDs {
			cube.led $i 0 $j
			::Stargate::Blink
			delay 50
			if {$i == $x && $j == $z} {
				# Engage chevron
				::Stargate::Blink 1
				delay 500
				cube.led $x 1 $z
				if {$x == 3} {
					cube.led 4 1 $z
					cube.led 4 0 $z
				}
				delay 100
				cube.led $x 2 $z
				if {$x == 3} { cube.led 4 2 $z }
				delay 100
				cube.led $x 0 $z 0 0 0
				delay 100
				cube.led $x 0 $z
				if {$x == 3} {
					# Chevron 7
					cube.led $i 0 $j 0 0 0
					cube.led $($i + 1) 0 $j 0 0 0
					break
				} else {
					delay $delay
				}
			}
			cube.led $i 0 $j 0 0 0
		}
	}
}

proc Stargate::Draw {} {
	cube.colour 0 13 50
	foreach {x z} $::Stargate::LEDs {
		cube.led $x 1 $z
		cube.led $x 2 $z
	}
}

set ::Stargate::tab [lrepeat 80 [lrepeat 4 [lrepeat 4 0]]]
loop i 0 80 { loop x 0 4 { loop z 0 4 {
	set y $(int(
		4 + 4 *
		sin(
			sqrt(pow(3.5 - $x, 2) + pow(3.5 - $z, 2)) / 1.3 +
			$i / 12.0
		)
	))
	lset ::Stargate::tab $i $x $z $y
}}}

proc Stargate::Sine {i} {
	cube.colour blue
	cube.begin -copy
	loop panel 0 6 {
		cube.panel $panel 0 0 0
	}
	loop x 0 4 { loop z 0 4 {
		set y [lindex $::Stargate::tab $i $x $z]
		set y $(5 - $y)
		cube.led $x		$y	$z
		cube.led $x		$y	$(7 - $z)
		cube.led $(7 - $x)	$y	$z
		cube.led $(7 - $x)	$y	$(7 - $z)
	}}
	cube.commit
}

proc Stargate::Engage {} {
	cube.colour blue
	loop i 5 -1 -1 {
		::Stargate::Sine 48
		loop panel 0 $i { cube.panel $panel 0 0 0 }
		delay 50
	}
	loop i 0 59 {
		::Stargate::Sine $i
		delay 7
	}
	loop i 0 7 {
		::Stargate::Sine 48
		loop panel 0 $i { cube.panel $panel 0 0 0 }
		delay 50
	}
	cube.colour -intensity 2 aqua
	loop x 1 7 { loop z 1 7 {
		if {[cube.lookup -distance $x $z] < 3} {
			cube.led $x 6 $z
			cube.led $x 7 $z
		}
	}}

	loop i 0 5 {
		cube.translate 0 -1 0
		delay 75
	}

	set white [cube.colour -get -intensity 2 white]
	loop i 0 1000 {
		set x [rand 8]
		set z [rand 8]
		if {[cube.lookup -distance $x $z] < 3} {
			cube.led $x 1 $z {*}$white
			cube.led $x 2 $z {*}$white
			delay 50
			cube.led $x 1 $z
			cube.led $x 2 $z
			delay 50
		}
	}
}

proc Stargate {} {
	cube.clear
	::Stargate::Draw
	sleep 5
	::Stargate::Dial 4000
	sleep 1
	loop i 0 5 {
		cube.translate 0 1 0
		delay 75
	}
	sleep 1
	::Stargate::Engage
}

if {!$interactive} { Stargate }

